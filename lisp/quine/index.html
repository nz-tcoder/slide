<!doctype html>
<html>
  <head>
    <meta charset="utf-8">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no">

    <title>クワインで遊ぶ</title>

    <link rel="stylesheet" href="../../css/reveal.css">
    <link rel="stylesheet" href="../../css/theme/black.css">

    <!--  -->
    <style type="text/css">
      .reveal h1,
      .reveal h2,
      .reveal h3,
      .reveal h4,
      .reveal h5,
      .reveal h6 {
      text-transform: none;
      }
    </style>

    <!-- Theme used for syntax highlighting of code -->
    <link rel="stylesheet" href="../../lib/css/zenburn.css">

    <style "text/css">
      .reveal .slides blockquote {
      text-align: left;
      }
    </style>
    <style "text/css">
      .reveal .slides p {
      text-align: left;
      }
    </style>
    
    <!-- Printing and PDF exports -->
    <script>
      var link = document.createElement( 'link' );
      link.rel = 'stylesheet';
      link.type = 'text/css';
      link.href = window.location.search.match( /print-pdf/gi ) ? 'css/print/pdf.css' : 'css/print/paper.css';
      document.getElementsByTagName( 'head' )[0].appendChild( link );
    </script>
  </head>
  <body>
    <div class="reveal">
      <div class="slides">
	<section data-markdown
		 data-separator="^\n---\n$"
		 data-separator-vertical="^\n--\n$">
## クワインで遊ぶ
### nz_tcoder

---

## 目次
* クワインとは？
* lispの例
* Cの例
* C → lisp
* 考察

---

## クワインとは？
ウィキペディアより引用

```
クワイン（英: Quine）は、コンピュータプログラムの一種で、
自身のソースコードと完全に同じ文字列を出力するプログラムである。
```

scheme/common lisp

```lisp
CL-USER> ((lambda (x) (list x (list 'quote x)))
	  '(lambda (x) (list x (list 'quote x))))
((LAMBDA (X) (LIST X (LIST 'QUOTE X))) '(LAMBDA (X) (LIST X (LIST 'QUOTE X))))
CL-USER> (equal * +)
T
```

---

## lispの例
バッククォートで書き直す

```lisp
((lambda (x) (list x (list 'quote x)))
	  '(lambda (x) (list x (list 'quote x))))
	  
((lambda (x) `(,x ',x))
  '(lambda (x) `(,x ',x)))
```

動作確認

```lisp
CL-USER> ((lambda (x) `(,x ',x))
	  '(lambda (x) `(,x ',x)))
((LAMBDA (X) `(,X ',X)) '(LAMBDA (X) `(,X ',X)))
CL-USER> (equal * +)
T
```

---

### バッククォートの出力は処理依存
ccl

```lisp
? ((lambda (x) `(,x ',x))
	  '(lambda (x) `(,x ',x)))
((LAMBDA (X) (LIST* X (LIST (LIST* 'QUOTE (LIST X))))) '(LAMBDA (X) (LIST* X (LIST (LIST* 'QUOTE (LIST X))))))
? (equal * +)
T
```

---

## lispの例
`lambda`→`let`

```lisp
CL-USER> ((lambda (x) `(,x ',x))
	  '(lambda (x) `(,x ',x)))
((LAMBDA (X) `(,X ',X)) '(LAMBDA (X) `(,X ',X)))
CL-USER> (let ((x '(lambda (x) `(,x ',x))))   ; letを使って同じ出力にする
	   `(,x ',x))
((LAMBDA (X) `(,X ',X)) '(LAMBDA (X) `(,X ',X)))
CL-USER> (let ((x '(lambda (x) `(,x ',x))))
	   `(let ((x ,x)) ',x))      ; letを挿入
(LET ((X (LAMBDA (X) `(,X ',X))))
  '(LAMBDA (X) `(,X ',X)))           ; -> 頭のquoteが邪魔
CL-USER> (let ((x '(lambda (x) `(,x ',x))))
	   `(let ((x ',x)) ,x))      ; quoteの位置を入れ替える
(LET ((X '(LAMBDA (X) `(,X ',X))))
  (LAMBDA (X) `(,X ',X)))
```

---

## lispの例
`let`版

```
(lambda (x) `(,x ',x)) を `(let ((x ',x)) ,x) に 置換
```

```
(let ((x '(lambda (x) `(,x ',x))))
  `(let ((x ',x)) ,x))
  
CL-USER> (let ((x '`(let ((x ',x)) ,x)))
	   `(let ((x ',x))
	      ,x))
(LET ((X
       '`(LET ((X ',X))
           ,X)))
  `(LET ((X ',X))
     ,X))
CL-USER> (equal * +)
T
```



---

## lispの例
```lisp
(let ((x '`(let ((x ',x)) ,x)))
  `(let ((x ',x))
     ,x))
```

xである必要はない。yでもzでも… 

x → let <!-- .element: class="fragment" -->

```lisp
CL-USER> (let ((let '`(let ((let ',let)) ,let)))
	   `(let ((let ',let))
	      ,let))
(LET ((LET
       '`(LET ((LET ',LET))
           ,LET)))
  `(LET ((LET ',LET))
     ,LET))
CL-USER> (equal * +)
T
```
<!-- .element: class="fragment" -->


---

## xである必要はない。

```lisp
((lambda (x) `(,x ',x))
   '(lambda (x) `(,x ',x)))
```

x → lambda <!-- .element: class="fragment" -->

```lisp
CL-USER> ((lambda (lambda) `(,lambda ',lambda))
	  '(lambda (lambda) `(,lambda ',lambda)))
((LAMBDA (LAMBDA) `(,LAMBDA ',LAMBDA)) '(LAMBDA (LAMBDA) `(,LAMBDA ',LAMBDA)))
CL-USER> (equal * +)
T
```
<!-- .element: class="fragment" -->

---

## Cの例
ウィキペディアより(実際は一行)

```C
int main() { char *s = "int main() { char *s = %c%s%c; printf(s, 34, s, 34); }";
printf(s, 34, s, 34); }
```

コンパイルと実行

```
$ cat quine.c
int main() { char *s = "int main() { char *s = %c%s%c; printf(s, 34, s, 34); }"; printf(s, 34, s, 34); }$
$gcc -o quine quine.c
quine.c: In function ‘main’:
quine.c:1: warning: incompatible implicit declaration of built-in function ‘printf’
$ ./quine > quine.out
$ cat quine.out 
int main() { char *s = "int main() { char *s = %c%s%c; printf(s, 34, s, 34); }"; printf(s, 34, s, 34); }$
$  cmp quine.c quine.out
$
```

---

## C → lisp
`printf`を使って書けるなら、`format`を使っても書けるはず。

```C
int main() { char *s = "int main() { char *s = %c%s%c; printf(s, 34, s, 34); }";
printf(s, 34, s, 34); }
```

```lisp
CL-USER> (let ((s "(let ((s ~s)) (format t s s))")) (format t s s))
(let ((s "(let ((s ~s)) (format t s s))")) (format t s s))
NIL
```

`~s` は文字列はダブルクォート付で出力する

lispなのでS式を出力するようにしてみる。<!-- .element: class="fragment" -->

---

## C → lisp

```lisp
(let ((s "(let ((s ~s)) (format t s s))")) (format t s s))

CL-USER> (let ((s "(let ((s ~s)) (read-from-string (format nil s s)))"))
           (read-from-string (format nil s s)))
(LET ((S "(let ((s ~s)) (read-from-string (format nil s s)))"))
  (READ-FROM-STRING (FORMAT NIL S S)))
100
CL-USER> (equal * +)
T
```

`read-from-string`はobjectとポジションを返す。

変数なしで出来ないか？ <!-- .element: class="fragment" -->

---

## C → lisp
文字列出力版

```lisp
CL-USER> (format t "(format t ~s ~:*~s)" "(format t ~s ~:*~s)")
(format t "(format t ~s ~:*~s)" "(format t ~s ~:*~s)")
NIL
```

`~:*` は一度消費した引数を再度使えるようにする。

---

## C → lisp
変数なし(S式出力版)

```lisp

CL-USER> (read-from-string (format nil "(read-from-string (format nil ~s ~:*~s))"
                                       "(read-from-string (format nil ~s ~:*~s))"))
(READ-FROM-STRING
 (FORMAT NIL "(read-from-string (format nil ~s ~:*~s))"
         "(read-from-string (format nil ~s ~:*~s))"))
117
CL-USER> (equal * +)
T
```


---

## 考察

クワインはevalについての不動点である。

```lisp
CL-USER> (setq p '((lambda (x) (list x (list 'quote x)))
		   '(lambda (x) (list x (list 'quote x)))))
CL-USER> (equal p (eval p))
T
CL-USER> 
```

Cでは文字列との格闘。lispでは文字列との格闘はほとんどない。

---

## 参考文献
1. ウィキペディア クワイン (プログラミング)
1. Let Over Lambda(Doug Hoyte著,エスアイビーアクセス社)<br>(英語版 https://letoverlambda.com/ )
1. 実践Common Lisp(Peter Seibel著,オーム社)<br>(英語版 http://www.gigamonkeys.com/book/ )
1. The Quine Page,http://www.nyx.net/~gthompso/quine.htm
	</section>
      </div>
    </div>
    
    <script src="../../lib/js/head.min.js"></script>
    <script src="../../js/reveal.js"></script>
    
    <script>
      // More info about config & dependencies:
      // - https://github.com/hakimel/reveal.js#configuration
      // - https://github.com/hakimel/reveal.js#dependencies
      Reveal.initialize({
      slideNumber:'c/t',
      width:'100%',
      heiht:'100%',
      margin:0.1,
      dependencies: [
      { src: '../../plugin/markdown/marked.js' },
      { src: '../../plugin/markdown/markdown.js' },
      { src: '../../plugin/notes/notes.js', async: true },
      { src: '../../plugin/highlight/highlight.js', async: true, callback: function() { hljs.initHighlightingOnLoad(); } }
      ]
      });
    </script>
  </body>
</html>
